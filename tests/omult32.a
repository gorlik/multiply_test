;-------------------------------------------------------------------------
;   _mulint.s - routine for 16x16 -> 16 multiplication (low bytes only) 
;
;   Copyright (C) 2025, Gabriele Gorla
;
;   This library is free software; you can redistribute it and/or modify it
;   under the terms of the GNU General Public License as published by the
;   Free Software Foundation; either version 2, or (at your option) any
;   later version.
;
;   This library is distributed in the hope that it will be useful,
;   but WITHOUT ANY WARRANTY; without even the implied warranty of
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;   GNU General Public License for more details.
;
;   You should have received a copy of the GNU General Public License
;   along with this library; see the file COPYING. If not, write to the
;   Free Software Foundation, 51 Franklin Street, Fifth Floor, Boston,
;   MA 02110-1301, USA.
;
;   As a special exception, if you link this library with other files,
;   some of which are compiled with SDCC, to produce an executable,
;   this library does not by itself cause the resulting executable to
;   be covered by the GNU General Public License. This exception does
;   not however invalidate any other reasons why the executable file
;   might be covered by the GNU General Public License.
;-------------------------------------------------------------------------

* = $0200

F1	= $02   ; 2 bytes
F2	= $04   ; 2 bytes
res	= $06   ; 2 bytes


; On Entry:
;   (F1, F1+1): multiplier 2 bytes
;   (F2, F2+1): multiplicand 2 bytes
; On Exit:
;   (res, res+1): 16 bit product

mult
	lda #0
	sta res+0
	ldy #8
loop1
	lsr F2+0
	bcc skip1
	clc
	tax
	lda res+0
	adc F1+0
	sta res+0
	txa
	adc F1+1
skip1
	asl F1+0
	rol F1+1
	dey
	bne loop1

loop2
	lsr F2+1
	bcc skip2
	clc
	adc F1+1
skip2
	asl F1+1
	bne loop2

	sta res+1
	rts
